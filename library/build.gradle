import net.danlew.android.joda.TzDataPlugin

apply plugin: 'com.android.library'

android {
    compileSdkVersion 36
    resourcePrefix 'joda_'

    defaultConfig {
        minSdkVersion 14
        consumerProguardFiles 'proguard-rules.txt'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    namespace 'net.danlew.android.joda'
    testNamespace 'net.danlew.android.joda.test'
    lint {
        lintConfig file('lint.xml')
        textOutput file('stdout')
        textReport true
    }

}

apply plugin: TzDataPlugin
apply plugin: "com.vanniktech.maven.publish"

repositories {
    google()
    mavenCentral()
}

dependencies {
    api(libs.jodaTime) {
        artifact {
            classifier = "no-tzdb"
            extension = "jar"
        }
    }

    implementation libs.androidx.startup
    implementation libs.androidx.annotation

    androidTestImplementation libs.junit
    androidTestImplementation libs.androidx.test.core
    androidTestImplementation libs.androidx.test.runner
    androidTestImplementation libs.androidx.test.rules
    androidTestImplementation libs.androidx.test.ext.junit
}

tzdata {
    tzDataDir = new File(project.rootDir, 'tzdata')
}

mavenPublishing {
    coordinates("net.danlew", "android.joda", "2.14.0-SNAPSHOT")

    pom {
        name = "joda-time-android Library"
        description = "Joda-Time library with Android specialization"
        inceptionYear = "2014"
        url = "https://github.com/dlew/joda-time-android"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "danlew"
                name = "Dan Lew"
                url = "https://github.com/dlew/"
            }
        }
        scm {
            url = "https://github.com/dlew/joda-time-android"
            connection = "scm:git@github.com:dlew/joda-time-android.git"
            developerConnection = "scm:git@github.com:dlew/joda-time-android.git"
        }
    }

    publishToMavenCentral()

    signAllPublications()
}
